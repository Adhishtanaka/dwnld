plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'at'
version '0.1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'at.dwnld'
    mainClass = 'at.dwnld.MainActivity'
}

javafx {
    version = "23.0.2"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

dependencies {

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'com.pixelduke:transit:2.0.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'

}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes(
                'Main-Class': 'at.dwnld.MainActivity'
        )
    }
}



test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'dwnld'
    }
    jpackage {
        installerType = 'msi'
        installerName = 'dwnld-installer'
        appVersion = '0.1.0'
        vendor = 'adhishtanaka'
        icon = 'src/main/resources/at/dwnld/icon.ico'
    }
}

jlinkZip {
    group = 'distribution'
}


//setup building command in my desktop
//jpackage --input build/libs --main-jar dwnld-0.1.0.jar --main-class at.dwnld.Main
//Activity --name dwnld --app-version 0.1.0 --vendor "Adhishtanaka" --icon src/main/r
//esources/at/dwnld/icon.ico --dest output --verbose --win-menu --win-menu-group "Dwn
//ld" --win-shortcut --java-options "--module-path \"C:\\openfx\\javafx-sdk-23.0.2\\lib\" --add-modules=javafx.controls,javafx.fxml,javafx.graphics,javafx.base"